# 3-stage membrane separation

# Ref.: W. Hock and K. Schittkowski, Test Examples for Nonlinear Programming
# Codes.  Lecture Notes in Economics and Mathematical Systems, v. 187,
# Springer-Verlag, New York, 1981, p. 124.
Model hs116 
  Parameters
    a = 0.002, > 0
    b = 1.262626, > 0
    c = 1.231059, > 0
    d = 0.03475, > 0
    e = 0.975, > 0
    f = 0.00975, > 0
  End Parameters

  Variables
    x[1] = 0.5, >=0
    x[2] = 0.8, >=0
    x[3] = 0.9, >=0
    x[4] = 0.1, >=0
    x[5] = 0.14, >=0
    x[6] = 0.5, >=0
    x[7] = 489, >=0
    x[8] = 80, >=0
    x[9] = 650, >=0
    x[10] = 450 , >=0
    x[11] = 150 , >=0
    x[12] = 150 , >=0
    x[13] = 150 , >=0
  End Variables

  Equations
    x[3] - x[2] >= 0
    x[2] - x[1] >= 0
    1 - a * x[7] + a * x[8] >= 0
    x[11] + x[12] + x[13] >= 50
    x[13] - b * x[10] + c * x[3] * x[10] >= 0
    x[5] - d * x[2] - e * x[2] * x[5] + f * x[2]^2 >= 0
    x[6] - d * x[3] - e * x[3] * x[6] + f * x[3]^2 >= 0
    x[4] - d * x[1] - e * x[1] * x[4] + f * x[1]^2 >= 0
    x[12] - b * x[9] + c * x[2] * x[9] >= 0
    x[11] - b * x[8] + c * x[1] * x[8] >= 0
    x[5] * x[7] - x[1] * x[8] - x[4] * x[7] + x[4] * x[8] >= 0
    1 - a * (x[2] * x[9] + x[5] * x[8] - x[1] * x[8] - x[6] * x[9]) - &
    x[5] - x[6] >= 0
    x[2] * x[9] - x[3] * x[10] - x[6] * x[9] - 500 * x[2] + &
    500 * x[6] + x[2] * x[10] >= 0
    x[2] - 0.9 - a * (x[2] * x[10] - x[3] * x[10]) >= 0
    x[11] + x[12] + x[13] <= 250
    
    0.1 <= x[1] <= 1
    0.1 <= x[2] <= 1
    0.1 <= x[3] <= 1
    0.0001 <= x[4] <= 0.1
    0.1 <= x[5] <= 0.9
    0.1 <= x[6] <= 0.9
    0.1 <= x[7] <= 1000
    0.1 <= x[8] <= 1000
    500 <= x[9] <= 1000
    0.1 <= x[10] <= 500
    1 <= x[11] <= 150
    0.0001 <= x[12] <= 150
    0.0001 <= x[13] <= 150

    ! best known objective = 97.588409
    minimize x[11] + x[12] + x[13]
  End Equations
End Model
