# Ref.: W. Hock and K. Schittkowski, Test Examples for Nonlinear Programming
# Codes.  Lecture Notes in Economics and Mathematical Systems, v. 187,
# Springer-Verlag, New York, 1981, p. 118.
Model hs109 
  Parameters
    a = 50.176
    b1= .25  
    b = sin(b1)
    c = cos(b1)

    l[1:2] = 0
    l[3:4] = -0.55
    l[5:7] = 196
    l[8:9] = -400

    u[1:2] = 1e20
    u[3:4] = 0.55
    u[5:7] = 252
    u[8:9] = 800
  End Parameters

  Variables
    x[1:9] = 0, >=l[1:9], <=u[1:9]
 
    ! optimal solution ???
    !    x[1] =  674.8881, >=l[1], <=u[1]
    !    x[2] =  1134.170, >=l[2], <=u[2]
    !    x[3] =  0.1335691, >=l[3], <=u[3]
    !    x[4] =  -0.3711526, >=l[4], <=u[4]
    !    x[5] =  252, >=l[5], <=u[5]
    !    x[6] =  252, >=l[6], <=u[6]
    !    x[7] =  201.465, >=l[7], <=u[7]
    !    x[8] =  426.661, >=l[8], <=u[8]
    !    x[9] =  368.494   , >=l[9], <=u[9]
  End Variables

  Equations
    x[4] - x[3] + .55 >= 0
    x[3] - x[4] + .55 >= 0
    2250000 - x[1]^2 - x[8]^2 >= 0
    2250000 - x[2]^2 - x[9]^2 >= 0
    x[5] * x[6] * sin(-x[3] - .25) + x[5] * x[7] * sin(-x[4] - .25) + &
        2 * b * x[5]^2 - a * x[1] + 400 * a = 0
    x[5] * x[6] * sin(x[3] - .25) + x[6] * x[7] * sin(x[3] - x[4] - .25) + &
        2 * b * x[6]^2 - a * x[2] + 400 * a = 0
    x[5] * x[7] * sin(x[4] - .25) + x[6] * x[7] * sin(x[4] - x[3] - .25) + &
        2 * b * x[7]^2 + 881.779 * a = 0
    a * x[8] + x[5] * x[6] * cos(-x[3] - .25) + &
        x[5] * x[7] * cos(-x[4] - .25) - 200 * a - 2 * c * x[5]^2 + &
        .7533e-3 * a * x[5]^2 = 0
    a * x[9] + x[5] * x[6] * cos(x[3] - .25) + &
        x[6] * x[7] * cos(x[3] - x[4] - .25) - 2 * c * x[6]^2 + &
        .7533e-3 * a * x[6]^2 - 200 * a = 0
     x[5] * x[7] * cos(x[4] - .25) + x[6] * x[7] * cos(x[4] - x[3] - .25) - &
        2 * c * x[7]^2 + 22.938 * a + .7533e-3 * a * x[7] ^2 = 0
     x[1:2] >= 0
     -.55 <= x[3:4] <= .55
     196 <= x[5:7] <= 252
     -400 <= x[8:9] <= 800                                     

    ! best known objective = 5362.06928
    minimize 3 * x[1] + 1e-6 * x[1]^3 + 2 * x[2] + .522074e-6 * x[2]^3
  End Equations
End Model
